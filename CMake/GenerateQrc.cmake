function(GenerateQrc QrcName Prefix Output)
    set(QrcFilepath ${Output}/${QrcName})
    file(GLOB_RECURSE QmlFiles "${CMAKE_CURRENT_SOURCE_DIR}/*.qml" "${CMAKE_CURRENT_SOURCE_DIR}/qmldir")
    if(QmlFiles)
        set(FileData "")
        set(FileData "${FileData}<RCC>\n")
        set(FileData "${FileData}    <qresource prefix=\"${Prefix}\">\n")
        foreach(file ${QmlFiles})
            file(RELATIVE_PATH RelativePath ${Output} ${file})
            file(RELATIVE_PATH RelativeSourcePath ${CMAKE_CURRENT_SOURCE_DIR} ${file})
            set(FileData "${FileData}        <file alias=\"${RelativeSourcePath}\">${RelativePath}</file>\n")
        endforeach()
        set(FileData "${FileData}    </qresource>\n")
        set(FileData "${FileData}</RCC>\n")
        file(WRITE ${QrcFilepath} "${FileData}")
        message(STATUS "Generated Qrc: ${QrcFilepath}")
    else()
        message(WARNING "Qrc isn't generated cause no one .qml file")
    endif()
endfunction()